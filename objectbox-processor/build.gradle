apply plugin: 'kotlin'

group = 'io.objectbox'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile project (':objectbox-code-modifier')
    compile project (':objectbox-generator')
    
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "io.objectbox:objectbox-java-api:0.9.12"
    compile 'io.objectbox:objectbox-java:0.9.12'
    // auto-service generates service configuration
    compileOnly 'com.google.auto.service:auto-service:1.0-rc2'

    // manually add tools.jar required by compile-testing
    testCompile files(org.gradle.internal.jvm.Jvm.current().getToolsJar())
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.32'
    testCompile 'com.google.testing.compile:compile-testing:0.11'
    // generated files during test need objectbox dependencies to compile
    testCompile 'io.objectbox:objectbox-java:0.9.12'
    testCompile 'io.objectbox:objectbox-daocompat:0.9.12'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'ObjectBox Processor'
                description 'Annotation processor for ObjectBox (NoSQL for Objects)'

                licenses {
                    license {
                        name 'GNU General Public License, Version 3'
                        url 'http://www.gnu.org/licenses/gpl.txt'
                        distribution 'repo'
                    }
                }

            }
        }
    }
}
