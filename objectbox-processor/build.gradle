apply plugin: 'kotlin'

group = 'io.objectbox'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    compile project (':objectbox-code-modifier')
    compile project (':objectbox-generator')
    
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "io.objectbox:objectbox-java-api:$objectbox_java_version"
    compile "io.objectbox:objectbox-java:$objectbox_java_version"
    // auto-service generates service configuration for annotation processor.
    def autoService = '1.0-rc6'
    compileOnly "com.google.auto.service:auto-service-annotations:$autoService"
    // Since Gradle 5.0 annotation processors are no longer detected on the compile classpath, 
    // so add to processor classpath instead.
    annotationProcessor "com.google.auto.service:auto-service:$autoService"
    // incap generates the META-INF descriptor required to enable incremental annotation processing with Gradle.
    def incap = '0.2'
    compileOnly "net.ltgt.gradle.incap:incap:$incap"
    annotationProcessor "net.ltgt.gradle.incap:incap-processor:$incap"

    // manually add tools.jar required by compile-testing
    def jvm = org.gradle.internal.jvm.Jvm.current()
    File toolsJar = jvm.getToolsJar()
    if(toolsJar != null) {
        testCompile files(toolsJar)
    }
    testCompile "junit:junit:$junit_version"
    // Note: compile-testing 0.18 does not support truth 0.46 or newer.
    testCompile 'com.google.truth:truth:0.45'
    testCompile 'com.google.testing.compile:compile-testing:0.18'
    // generated files during test need objectbox dependencies to compile
    testCompile "io.objectbox:objectbox-java:$objectbox_java_version"
}

apply from: rootProject.file("gradle/objectbox-publish.gradle")

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from 'README'
}

artifacts {
    // jar added by java plugin.
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'ObjectBox Processor'
                description 'Annotation processor for ObjectBox (NoSQL for Objects)'

                licenses {
                    license {
                        name 'GNU General Public License, Version 3'
                        url 'http://www.gnu.org/licenses/gpl.txt'
                        distribution 'repo'
                    }
                }

            }
        }
    }
}
