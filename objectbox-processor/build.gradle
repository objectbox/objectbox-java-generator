apply plugin: 'kotlin'

group = 'io.objectbox'

sourceCompatibility = 1.7

dependencies {
    compile project (':objectbox-code-modifier')
    compile project (':objectbox-generator')
    
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "io.objectbox:objectbox-java-api:$objectbox_java_version"
    compile "io.objectbox:objectbox-java:$objectbox_java_version"
    // auto-service generates service configuration
    compileOnly 'com.google.auto.service:auto-service:1.0-rc2'

    // manually add tools.jar required by compile-testing
    def jvm = org.gradle.internal.jvm.Jvm.current()
    File toolsJar = jvm.getToolsJar()
    if(toolsJar == null) {
        def msg = 'No tools.jar on the JVM path: ' + jvm.getJavaHome()
        logger.warn(msg)
        def candidate = new File('C:\\Program Files\\Java\\jdk-10\\lib\\tools.jar')
        if(candidate.exists) {
            logger.info("Found $candidate")
            toolsJar = candidate
        }
        if(toolsJar == null) {
            throw new GradleException(msg)
        }
    }
    testCompile files(toolsJar)
    testCompile 'junit:junit:4.12'
    testCompile 'com.google.truth:truth:0.37'
    testCompile 'com.google.testing.compile:compile-testing:0.15'
    // generated files during test need objectbox dependencies to compile
    testCompile "io.objectbox:objectbox-java:$objectbox_java_version"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from 'README'
}

artifacts {
    archives jar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'ObjectBox Processor'
                description 'Annotation processor for ObjectBox (NoSQL for Objects)'

                licenses {
                    license {
                        name 'GNU General Public License, Version 3'
                        url 'http://www.gnu.org/licenses/gpl.txt'
                        distribution 'repo'
                    }
                }

            }
        }
    }
}
