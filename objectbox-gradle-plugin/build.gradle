// https://docs.gradle.org/current/userguide/custom_plugins.html
apply plugin: 'kotlin'
apply plugin: 'groovy' // for dsl tests
apply plugin: 'maven'

group = 'io.objectbox'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

def genResourcesDir = new File(buildDir, 'src/generated/resources')
def versionFile = new File(genResourcesDir, 'io/objectbox/gradle/version.properties')

sourceSets {
    main {
        resources {
            srcDir genResourcesDir
        }
    }
}

dependencies {
    compile gradleApi()
    compile project(':objectbox-code-modifier')
    compileOnly "com.android.tools.build:gradle:$android_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'org.javassist:javassist:3.21.0-GA'

    // For Transformer to resolve class dependencies to (real) ObjectBox classes
    compile "io.objectbox:objectbox-java:$objectbox_java_version"
    compile "io.objectbox:objectbox-java-api:$objectbox_java_version"

    // For Gradle test projects (dir "test-gradle-projects")
    testCompile "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    testCompile "com.android.tools.build:gradle:$android_version"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.7.22'
    testCompile gradleTestKit()
}

// puts artifact version to properties file inside resources
task generateVersionProperties {
    inputs.property 'version', project.version
    outputs.file versionFile

    doLast {
        versionFile.parentFile.mkdirs()
        versionFile.withWriter {
            it.write """# Generated by gradle task generateVersionProperties
version=${project.version}
"""
        }
    }
}

tasks['compileKotlin'].dependsOn generateVersionProperties

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'README'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from 'README'
}

artifacts {
    archives jar
    archives javadocJar
    // Not yet: archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'ObjectBox Gradle Plugin'
                description 'Gradle Plugin for ObjectBox (NoSQL for Objects)'

                licenses {
                    license {
                        name 'GNU General Public License, Version 3'
                        url 'http://www.gnu.org/licenses/gpl.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")
    def files = sourceSets.test.runtimeClasspath

    inputs.files files
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = files.join("\n")
    }
}

// Works with gradle build, but not in IntelliJ -> run manually
tasks['test'].dependsOn createClasspathManifest

//
//dependencies {
//    testRuntime files(createClasspathManifest)
//}