apply plugin: "java"
apply plugin: "kotlin"

archivesBaseName = "objectbox-generator"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "io.objectbox:objectbox-java:$objectbox_java_version"
    // https://freemarker.apache.org/docs/app_versions.html
    implementation "org.freemarker:freemarker:2.3.31"
    implementation "org.greenrobot:essentials:$essentials_version"

    testImplementation "junit:junit:$junit_version"
}

test {
    doFirst {
        mkdir "test-out"
    }
    doLast {
        delete "test-out"
    }
}

javadoc {
    failOnError = false
    title = "ObjectBox Generator ${version} API"
    // Unfinished APIs:
    options.bottom = /*"Available under the GPLv3 - */ "<i>Copyright &#169; 2022 <a href=\"https://objectbox.io/\">ObjectBox Ltd</a>. All Rights Reserved.</i>"
    doLast {
        copy {
            from "../javadoc-style/"
            into "build/docs/javadoc/"
        }
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = "javadoc"
    from "build/docs/javadoc"
}

task sourcesJar(type: Jar) {
    archiveClassifier = "sources"
//    from sourceSets.main.allSource
    from "README"
}

apply from: rootProject.file("gradle/objectbox-publish.gradle")
// Set project-specific properties
publishing.publications {
    mavenJava(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        pom {
            name = "ObjectBox Generator"
            description = "Code generator for ObjectBox, the superfast NoSQL database for Objects"
        }
    }
}
