buildscript {
    ext {
        // Kotlin version must match the one shipped with Gradle (see output of `gradlew --version`)
        // to avoid conflicts when compiling our Gradle plugin. https://youtrack.jetbrains.com/issue/KT-38010
        kotlin_version = '1.4.20' // Gradle 6.8.3

        android_version = '3.6.3' // See README for minimal supported version. http://google.github.io/android-gradle-dsl/javadoc/
        essentials_version = '3.1.0'
        junit_version = '4.13.2' // https://junit.org/junit4/
        // Note: truth 1.1.2 breaks Android plugin apply test.
        truth_version = '1.0' // https://github.com/google/truth/releases
        mockito_version = '3.8.0' // https://github.com/mockito/mockito/releases
        moshi_version = '1.11.0'

        // Typically, only edit those two:
        def versionNumber = '3.0.1' // Without "-SNAPSHOT", e.g. '2.5.0' or '2.4.0-RC'.
        def isRelease = false       // Set to true for releasing to ignore versionPostFix to avoid e.g. "-dev" versions.

        def libsRelease = isRelease  // e.g. diverge if plugin is still SNAPSHOT, but libs are already final
        def libsVersion = versionNumber + (libsRelease ? "" : "-dev-SNAPSHOT")
        def libsSyncVersion = versionNumber + (libsRelease ? "" : "-sync-SNAPSHOT")

        // Calculate version codes.
        def versionPostFix
        if (isRelease) {
            versionPostFix = ""
        } else if (project.hasProperty('versionPostFix')) {
            versionPostFix = "-${project.versionPostFix}-SNAPSHOT"
        } else {
            versionPostFix = "-dev-SNAPSHOT"
        }

        objectbox_plugin_version = versionNumber + versionPostFix // Artifact versions of this project.
        objectbox_java_version = libsVersion // Java library used by sub-projects.
        applies_ob_java_version = libsVersion // Java library added to projects applying the plugin.
        applies_ob_native_version = libsVersion // Native library added to projects applying the ObjectBoxGradlePlugin.
        applies_ob_native_sync_version = libsSyncVersion // Native library added to projects applying the ObjectBoxSyncGradlePlugin.

        println "version=$objectbox_plugin_version"
        println "objectbox_java_version=$objectbox_java_version"
        println "applies_ob_java_version=$applies_ob_java_version"
        println "ObjectBoxGradlePlugin:"
        println "  applies_ob_native_version=$applies_ob_native_version"
        println "ObjectBoxSyncGradlePlugin:"
        println "  applies_ob_native_sync_version=$applies_ob_native_sync_version\n"

        // Internal Maven repo: used in all projects, printing info/warning only once here.
        hasInternalObjectBoxRepo = project.hasProperty('gitlabUrl')
        if (hasInternalObjectBoxRepo) {
            println "gitlabUrl=$gitlabUrl added to repositories."
        } else {
            println "WARNING: gitlabUrl missing from gradle.properties."
        }
    }

    repositories {
        mavenCentral()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.8" // for code-modifier, gradle-plugin
        classpath "com.github.ben-manes:gradle-versions-plugin:0.38.0"
        classpath "io.github.gradle-nexus:publish-plugin:1.1.0"
    }
}

apply plugin: "com.github.ben-manes.versions"
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}


allprojects {
    group = 'io.objectbox'
    version = "$objectbox_plugin_version"

    // Note: also update IncrementalCompilationTest.projectSetup as needed.
    repositories {
        mavenCentral()
        google()
        if (hasInternalObjectBoxRepo) {
            maven {
                url "$gitlabUrl/api/v4/groups/objectbox/-/packages/maven"
                name "GitLab"
                credentials(HttpHeaderCredentials) {
                    name = project.hasProperty("gitlabTokenName") ? gitlabTokenName : "Private-Token"
                    value = project.hasProperty("gitlabToken") ? gitlabToken : gitlabPrivateToken
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
        mavenLocal()
    }

    configurations.all {
        // Projects are using snapshot dependencies that may update more often than 24 hours.
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }
}

task cleanOut {
    description 'Clears IntelliJ build folders "out/"'
    doLast {
        project.allprojects {
            def outDir = file("out")
            if(outDir.isDirectory()) {
                def deleted = delete outDir
                println "Deleted dir \"${outDir.getAbsolutePath()}\": $deleted"
            }
        }
    }
}

wrapper {
    distributionType Wrapper.DistributionType.ALL
}

// Plugin to publish to Central https://github.com/gradle-nexus/publish-plugin/
// This plugin ensures a separate, named staging repo is created for each build when publishing.
apply plugin: "io.github.gradle-nexus.publish-plugin"
nexusPublishing {
    repositories {
        sonatype {
            // Staging profile ID for io.objectbox is 1c4c69cbbab380
            // Get via https://oss.sonatype.org/service/local/staging/profiles
            // or with Nexus Staging Plugin getStagingProfile task.
            stagingProfileId = "1c4c69cbbab380"
            if (project.hasProperty("sonatypeUsername") && project.hasProperty("sonatypePassword")) {
                println('nexusPublishing credentials supplied.')
                username = sonatypeUsername
                password = sonatypePassword
            } else {
                println('nexusPublishing credentials NOT supplied.')
            }
        }
    }
}
