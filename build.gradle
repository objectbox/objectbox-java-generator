buildscript {
    ext {
        android_version = '3.4.0' // Minimal supported version is 3.0.0. http://google.github.io/android-gradle-dsl/javadoc/
        kotlin_version = '1.3.41'
        mockito_version = '2.25.1'

        // version post fix: '-<value>' or '' if not defined
        def versionPostFixValue = project.findProperty('versionPostFix')
        def versionPostFix = versionPostFixValue ? "-$versionPostFixValue" : ''
        
        objectbox_plugin_version = "2.4.0$versionPostFix-SNAPSHOT" // plugin, processor, generator and code-modifier version
        def dependencies_version = '2.4.0-dev-SNAPSHOT'
        objectbox_java_version = dependencies_version // Java version used by code and tests
        objectbox_runtime_version = dependencies_version // The plugin adds dependencies with this version to projects applying it

        println "Version $objectbox_plugin_version with dependency on Java $objectbox_java_version. " +
                "Adds dependencies with version $objectbox_runtime_version."

        // Internal Maven repo: used in all projects, printing info/warning only once here.
        hasInternalObjectBoxRepo = project.hasProperty('internalObjectBoxRepo')
        if (hasInternalObjectBoxRepo) {
            println("Using internal ObjectBox repository $internalObjectBoxRepo.")
        } else {
            println "Warning: internalObjectBoxRepo, internalObjectBoxRepoUser and internalObjectBoxRepoPassword missing from gradle.properties."
        }
    }

    repositories {
        jcenter()
        mavenCentral()
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.8" // for code-modifier, gradle-plugin
        classpath "com.github.ben-manes:gradle-versions-plugin:0.21.0"
    }
}

apply plugin: "com.github.ben-manes.versions"
apply from: 'gradle/objectbox-deploy.gradle'

subprojects {
    version = "$objectbox_plugin_version"

    repositories {
        jcenter()
        mavenCentral()
        google()
        if (hasInternalObjectBoxRepo) {
            maven {
                credentials {
                    username internalObjectBoxRepoUser
                    password internalObjectBoxRepoPassword
                }
                url internalObjectBoxRepo
            }
        }
        mavenLocal()
    }

    configurations.all {
        // Projects are using snapshot dependencies that may update more often than 24 hours.
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
        }
    }
}

task cleanOut {
    description 'Clears IntelliJ build folders "out/"'
    doLast {
        project.allprojects {
            def outDir = file("out")
            if(outDir.isDirectory()) {
                def deleted = delete outDir
                println "Deleted dir \"${outDir.getAbsolutePath()}\": $deleted"
            }
        }
    }
}

wrapper {
    distributionType Wrapper.DistributionType.ALL
}
