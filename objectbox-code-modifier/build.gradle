apply plugin: 'kotlin'
apply plugin: 'de.fuerstenau.buildconfig'
apply plugin: 'idea' // to pick up generated BuildConfig in IntelliJ

group = 'io.objectbox'
archivesBaseName = 'objectbox-code-modifier'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.greenrobot:essentials:3.0.0-RC1'
    compile 'com.squareup.moshi:moshi:1.8.0'

    compile "io.objectbox:objectbox-java-api:$objectbox_java_version"
    compile project (':objectbox-generator')

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:$mockito_version"
    testCompile 'com.natpryce:hamkrest:1.6.0.0'
}

buildConfig {
    // rename to avoid conflict with other build config files (modules use same root package)
    clsName = 'CodeModifierBuildConfig'
}

apply from: rootProject.file("gradle/objectbox-publish.gradle")

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'README'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from 'README'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            // Basic definitions are defined in root project
            pom.project {
                name 'ObjectBox Code Modifier'
                description 'Code modifier for ObjectBox (NoSQL for Objects)'

                licenses {
                    license {
                        name 'GNU General Public License, Version 3'
                        url 'http://www.gnu.org/licenses/gpl.txt'
                        distribution 'repo'
                    }
                }

            }
        }
    }
}
