apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.fuerstenau.buildconfig'
apply plugin: 'idea' // to pick up generated BuildConfig in IntelliJ

archivesBaseName = 'objectbox-code-modifier'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.greenrobot:essentials:$essentials_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    implementation "io.objectbox:objectbox-java:$objectbox_java_version"
    implementation project (':objectbox-generator')

    testImplementation "junit:junit:$junit_version"
    testImplementation "com.google.truth:truth:$truth_version"
}

buildConfig {
    // rename to avoid conflict with other build config files (modules use same root package)
    clsName = 'CodeModifierBuildConfig'
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from 'README'
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from 'README'
}

apply from: rootProject.file("gradle/objectbox-publish.gradle")
// Set project-specific properties
publishing.publications {
    mavenJava(MavenPublication) {
        from components.java
        artifact sourcesJar
        artifact javadocJar
        pom {
            name = 'ObjectBox Code Modifier'
            description = 'Code modifier for ObjectBox (NoSQL for Objects)'
        }
    }
}
