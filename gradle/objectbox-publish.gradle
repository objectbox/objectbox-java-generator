// Configures common publishing settings

apply plugin: "maven"
apply plugin: "signing"

configurations {
    deployerJars
}

dependencies {
    // Using an older version to remain compatible with Wagon API used by Gradle/Maven
    deployerJars 'org.apache.maven.wagon:wagon-webdav-jackrabbit:3.2.0'
    deployerJars 'org.apache.maven.wagon:wagon-ftp:3.3.2'
}

javadoc {
    if(JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

def hasSigningProperties() {
    return (project.hasProperty('signingKeyId')
            && project.hasProperty('signingKeyFile')
            && project.hasProperty('signingPassword'))
}

signing {
    if (hasSigningProperties()) {
        String signingKey = new File(signingKeyFile).text
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign configurations.archives
    } else {
        println "WARNING: Signing information missing/incomplete for ${project.name}"
    }
}

// Use afterEvaluate or dependencies might be lost in the generated POM
afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {
                def preferredRepo = project.findProperty('preferredRepo')
                println "preferredRepo=$preferredRepo"

                if (preferredRepo == 'local') {
                    repository url: repositories.mavenLocal().url
                    println "Uploading archives to mavenLocal()."
                } else if (preferredRepo != null
                        && project.hasProperty('preferredUsername')
                        && project.hasProperty('preferredPassword')) {
                    if (!hasSigningProperties()) {
                        throw new InvalidUserDataException("To upload to repo signing is required.")
                    }

                    configuration = configurations.deployerJars

                    // replace placeholders
                    def repositoryUrl = preferredRepo
                            .replace('__groupId__', project.group)
                            .replace('__artifactId__', project.archivesBaseName)
                    repository(url: repositoryUrl) {
                        authentication(userName: preferredUsername, password: preferredPassword)
                    }
                    println "Uploading archives to $repositoryUrl."
                } else if (project.hasProperty('sonatypeUsername')
                        && project.hasProperty('sonatypePassword')) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                    def isSnapshot = version.endsWith('-SNAPSHOT')
                    def sonatypeRepositoryUrl = isSnapshot ?
                            "https://oss.sonatype.org/content/repositories/snapshots/"
                            : "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    repository(url: sonatypeRepositoryUrl) {
                        authentication(userName: sonatypeUsername, password: sonatypePassword)
                    }
                    println "Uploading archives to $sonatypeRepositoryUrl."
                } else {
                    println "Deployment settings missing/incomplete for ${project.name}."
                }

                // Common properties, projects still need to set name, description and license
                pom.project {
                    packaging 'jar'
                    url 'https://objectbox.io'

                    scm {
                        url 'https://github.com/objectbox/objectbox-java'
                        connection 'scm:git@github.com:objectbox/objectbox-java.git'
                        developerConnection 'scm:git@github.com:objectbox/objectbox-java.git'
                    }

                    developers {
                        developer {
                            id 'ObjectBox'
                            name 'ObjectBox'
                        }
                    }

                    issueManagement {
                        system 'GitHub Issues'
                        url 'https://github.com/objectbox/objectbox-java/issues'
                    }

                    organization {
                        name 'ObjectBox Ltd.'
                        url 'https://objectbox.io'
                    }
                }
            }
        }
    }
}
